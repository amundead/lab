//Jenkins scripts CI/CD by Amir@Mindef 2024 v2.03

pipeline {
    agent any

    environment {
        HARBOR_REGISTRY = 'bakul.mod.gov.my'  // Harbor registry URL
        HARBOR_PROJECT = 'testing-app/node-helloworld:v1.02'  // Harbor project where image will be pushed
        NAME_DEPLOY = 'node-helloworld'
        PATH_YAML = "deploy-dev/deploy_bakul.yaml"
        NAMES_SPACE = "dev-app-awam"  // Namespace for deployment in k8s
        IMAGE_NAME_HARBOR = "${HARBOR_REGISTRY}/${HARBOR_PROJECT}"  // Full image name for Harbor
        KUBECONFIG = "/home/jenkins/agent/k8s-dev/k8s-dev.yaml"  // Path to your KUBECONFIG certs
        DOCKERFILE_LOCATION = './Dockerfile'    // Dockerfile in root location  './tot/lab-01/Dockerfile'
    }
    stages {
        stage('Clone Repository') {
            steps {
                git branch: 'main', url: "https://github.com/amundead/test-repo.git"  // Clone your GitHub repo
            }
        }
        

        stage('Check Dockerfile Location') {
            steps {
                script {
                    // Check if the Dockerfile exists in the root location
                    if (fileExists(DOCKERFILE_LOCATION)) {
                        echo "Dockerfile found at ${DOCKERFILE_LOCATION}"
                    } else {
                        error "Dockerfile not found at ${DOCKERFILE_LOCATION}"
                    }
                }
            }
        }

        stage('Build Docker Image') {
            steps {
                script {
                    // Build Docker image using docker.build with --no-cache option
                    docker.build("${IMAGE_NAME_HARBOR}", "--no-cache -f ${DOCKERFILE_LOCATION} .")
                }
            }
        }

        stage('Tag and Push Docker Image to Harbor') {
            steps {
                script {
                    // Use docker.withRegistry for secure login and push to Harbor
                    docker.withRegistry("https://${HARBOR_REGISTRY}", 'harbor-credentials-amir') {
                        docker.image("${IMAGE_NAME_HARBOR}").push()
                    }
                }
            }
        }

        stage('Check Files') {
            steps {
                sh 'ls -R'  // List all files in the workspace to verify the structure
            }
        }

        stage('Update Deployment YAML') {
            steps {
                // Replace the placeholder with the actual Docker image name
                sh "sed -i 's|{{DOCKER_IMAGE}}|$IMAGE_NAME_HARBOR|g' ${PATH_YAML}"
                sh "sed -i 's|{{NAME_DEPLOY}}|$NAME_DEPLOY|g' ${PATH_YAML}"
                sh "sed -i 's|{{NAMES_SPACE}}|$NAMES_SPACE|g' ${PATH_YAML}"
            }
        }
      
        stage('Update or Restart Deployment in Kubernetes') {
            steps {
                script {
                    // Check if the deployment already exists
                    def deploymentExists = sh(script: "kubectl --kubeconfig=$KUBECONFIG get deployment $NAME_DEPLOY --namespace=$NAMES_SPACE", returnStatus: true) == 0

                    if (deploymentExists) {
                        // Check the currently deployed image tag
                        def currentImage = sh(script: "kubectl --kubeconfig=$KUBECONFIG get deployment $NAME_DEPLOY --namespace=$NAMES_SPACE -o jsonpath='{.spec.template.spec.containers[0].image}'", returnStdout: true).trim()

                        if (currentImage != "$IMAGE_NAME_HARBOR") {
                            // If the current image is different, update with kubectl set image
                            echo "New image detected: $IMAGE_NAME_HARBOR. Updating the deployment..."
                            sh "kubectl --kubeconfig=$KUBECONFIG set image deployment/$NAME_DEPLOY $NAME_DEPLOY=$IMAGE_NAME_HARBOR --namespace=$NAMES_SPACE"
                        } else {
                            // If the image is the same, perform a rollout restart
                            echo "Same image detected. Performing rollout restart..."
                            sh "kubectl --kubeconfig=$KUBECONFIG rollout restart deployment/$NAME_DEPLOY --namespace=$NAMES_SPACE"
                        }
                    } else {
                        // If the deployment does not exist, apply the deployment YAML
                        echo "Deployment does not exist. Applying the deployment YAML..."
                        sh "kubectl --kubeconfig=$KUBECONFIG apply -f ${PATH_YAML} --namespace=$NAMES_SPACE"
                    }
                }
            }
        }


        stage('Show Deployment Services') {
            steps {
                sh "kubectl --kubeconfig=$KUBECONFIG get svc -n $NAMES_SPACE $NAME_DEPLOY-loadbalancer"  // Get the LoadBalancer IP of service.
            }
        }
    }
    
    post {
        success {
            echo 'Deployment to Kubernetes cluster successful!'
        }
        failure {
            echo 'Deployment failed! haha'
        }
    }
}
